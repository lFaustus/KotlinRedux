buildscript {
  project.apply from: "${rootDir}/constants.gradle"

  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"

    // https://github.com/palantir/gradle-jacoco-coverage
    classpath 'com.palantir:jacoco-coverage:0.4.0'
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.11'
  id 'com.github.kt3k.coveralls' version '2.8.2'
}

repositories {
  jcenter()
}

apply plugin: "com.palantir.jacoco-full-report"

subprojects {
  repositories {
    jcenter()
  }

  apply plugin: "kotlin"
  apply plugin: "jacoco"

  project.apply from: "${rootDir}/constants.gradle"

  configurations.all {
    // Make sure the latest JitPack releases are used.
    resolutionStrategy.cacheChangingModulesFor 24, 'hours'
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = project.ext.jvmTarget
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = '1.8'
    }
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.testng:testng:$testNG"
  }

  test {
    testLogging  {
      events=["skipped", "failed"]
      exceptionFormat="full"

      debug.events = ["skipped", "failed"]
      debug.exceptionFormat="full"

      info.events = ["failed", "skipped"]
      info.exceptionFormat="full"

      warn.events = ["failed", "skipped"]
      warn.exceptionFormat="full"
    }

    maxHeapSize = project.ext.testHeapSize
    useTestNG()
  }
}

project('core') {
  configurations { testArtifacts.extendsFrom testRuntime }

  task testJar (type: Jar) {
    classifier "test"
    from sourceSets.test.output
  }

  artifacts {
    testArtifacts testJar
  }

  dependencies {
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project('middleware') {
  dependencies {
    api project(':common:core')
    testApi project(':common:simple-store')
  }
}

project('router') {
  dependencies {
    api project(':common:core')
    api project(':common:middleware')
    testApi project(':common:simple-store')
  }
}

project('common-saga') {
  configurations { testArtifacts.extendsFrom testRuntime }

  task testJar (type: Jar) {
    classifier "test"
    from sourceSets.test.output
  }

  artifacts {
    testArtifacts testJar
  }

  dependencies {
    api project(':common:core')
    api project(':common:middleware')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito"
  }
}

project('rx-saga') {
  dependencies {
    api project(':common:core')
    api project(':common:middleware')
    api project(':common:common-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
    testApi project (path: ":common:common-saga", configuration: 'testArtifacts')
  }
}

project('simple-store') {
  dependencies {
    api project(':common:core')
    testApi project (path: ":common:core", configuration: 'testArtifacts')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project('async-store') {
  dependencies {
    api project(':common:core')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testApi project (path: ":common:core", configuration: 'testArtifacts')
    testApi project(':common:simple-store')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project('final-store') {
  dependencies {
    api project(':common:core')
    api project(':common:simple-store')
    api project(':common:async-store')
    testApi project (path: ":common:core", configuration: 'testArtifacts')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
  }
}

project('ui') {
  dependencies {
    api project(':common:core')
    testApi project(':common:simple-store')
  }
}
