buildscript {
  project.apply from: "${rootDir}/constants.gradle"
  project.apply from: "${rootDir}/android/constants.gradle"

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"
    classpath "com.android.tools.build:gradle:3.3.0"
    classpath "com.github.dcendents:android-maven-gradle-plugin:$androidMavenGradle"
  } 
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

repositories {
  google()
  jcenter()
  maven { url = "https://jitpack.io" }
}

/** Jitpack support */
subprojects {
  apply plugin: 'com.github.dcendents.android-maven'
}

subprojects {
  repositories {
    google()
    jcenter()
    maven { url = "https://jitpack.io" }
  }
  
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'

  project.apply from: "${rootDir}/constants.gradle"
  project.apply from: "${rootDir}/android/constants.gradle"
  group = 'com.github.protoman92'
  version '1.0-SNAPSHOT'

  configurations.all {
    // Make sure the latest JitPack releases are used.
    resolutionStrategy.cacheChangingModulesFor 24, 'hours'
  }

  android {
    compileSdkVersion compileSdk

    defaultConfig {
      minSdkVersion minSdk
      targetSdkVersion targetSdk
      versionCode versionCode
      versionName versionName

      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
      release {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
    }
  }

  dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin"
    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test:runner:$testRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunit"
  }

  test {
    maxHeapSize = project.ext.testHeapSize
  }
}

project(':android:android-util') {}

project(':android:android-ui') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    api project(':android:android-util')
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    testApi project (path: ":common:common-ui", configuration: 'testArtifacts')
  }
}

project(':android:android-lifecycle') {
  dependencies {
    api project(':common:common-core')
    api project(':common:common-ui')
    implementation "androidx.appcompat:appcompat:$appCompat"
  }
}

project(':android:android-recyclerview') {
  dependencies {
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
  }
}

project(':android:android-core-rx-saga') {
  dependencies {
    api project(':common:common-saga')
    api project(':common:common-rx-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
  }
}

project(':android:android-livedata-rx-saga') {
  dependencies {
    api project(':common:common-saga')
    api project(':common:common-rx-saga')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinCoroutines"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle"
    testImplementation "androidx.arch.core:core-testing:$androidxCoreTest"
  }
}

project(':android:android-router') {
  dependencies {
    api project(':common:common-core')
    api project(':android:android-util')
    implementation "androidx.appcompat:appcompat:$appCompat"
  }
}

project(':android:android-all') {
  dependencies {
    api project(':common:common-all')
    api project(':android:android-ui')
    api project(':android:android-lifecycle')
    api project(':android:android-recyclerview')
    api project(':android:android-router')
    api project(':android:android-core-rx-saga')
    api project(':android:android-livedata-rx-saga')
  }
}
